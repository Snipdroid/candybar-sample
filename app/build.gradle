apply plugin: 'com.android.application'

android {
    compileSdk = rootProject.ext.CompileSdk

    namespace 'com.candybar.sample'

    defaultConfig {
        applicationId 'com.candybar.sample'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 1
        versionName '1.0'
    }

    bundle {
        language {
            enableSplit false
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError false
    }
}

base {
    // Output file name
    archivesName = "${parent.name}-${android.defaultConfig.versionCode}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    /*
     * CandyBar Material Dashboard Library
     *
     * TODO: How to update the dashboard?!
     * Check this update guide for every version
     *      - https://github.com/zixpo/candybar-sample/wiki/Updating-the-dashboard
     * If you've missed some versions you've to also follow update guide for those versions
     */

    // Using Snipdroid fork with statistics service integration
    // For multi-module projects, specify the module name after the repo
    implementation 'com.github.Snipdroid:candybar:main-SNAPSHOT'

    // For production after release:
    // implementation 'com.github.Snipdroid.candybar:library:VERSION'

}

// Code for automatically copying appfilter.xml and drawable.xml
// from `res/xml` directory to `assets` directory
tasks.register('copyXMLs') {
    ['appfilter.xml', 'drawable.xml'].each { file ->
        copy {
            from "$projectDir/src/main/res/xml/$file"
            into "$projectDir/src/main/assets/"
        }
    }
}

tasks.configureEach { task ->
    if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
        task.dependsOn copyXMLs
    }
}
